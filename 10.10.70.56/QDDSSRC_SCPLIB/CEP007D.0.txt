     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : CEP007D                                    *
     A*    AUTHOR       : A1491  EASON                               *
     A*    CREATE DATE  : 97/09/11                                   *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : CEP007                                     *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :主檔資料維護子系統                        *
     A*    REMARK       :通路產品對照維護                          *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*----------------------------------------------------------------
     A          R SFLSR                     SFL
     A  54                                  SFLNXTCHG
     A            RRN            4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '2' '4' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            EG03      R        O 10  7
     A            SNC05     R        O 10 17REFFLD(NC05 MANCPF)
     A            SEG04     R   -2   O 10 37REFFLD(EG04)
     A                                      EDTWRD('  /  /  ')
     A            SEG05     R   -2   O 10 47REFFLD(EG05)
     A                                      EDTWRD('  /  /  ')
     A            EG06      R        O 10 58
     A            EG01      R        H
     A            EG02      R        H
     A            EG04      R        H
     A*----------------------------------------------------------------
     A          R SFLCR                     SFLCTL(SFLSR)
     A                                      SFLSIZ(0040)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF05(05)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND(*MORE)
     A  97                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN           4S 0H      SFLRCDNBR
     A                                  1  2'CEP007'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 31'通路產品對照維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  5  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  5 14REFFLD(EG01)
     A            S#B03     R   -6   O  5 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  5 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  5 43REFFLD(EG02)
     A            SND04     R        O  5 46REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                  6  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  6 18'2=修改'
     A                                      COLOR(BLU)
     A                                  6 27'4=刪除'
     A                                      COLOR(BLU)
     A                                  6 36'5=查詢'
     A                                      COLOR(BLU)
     A                                  8  4'A 公司產品代號／名稱          +
     A                                      有效起日有效訖日 +
     A                                      通路產品代號'
     A                                      DSPATR(UL)
     A                                      COLOR(BLU)
     A            DBGN3     R        B  9  7REFFLD(EG03)
      *----------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                    +2'F6 =新增'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*----------------------------------------------------------------
     A          R DSPD2
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CF04(04)
     A                                      CF09(09)
     A                                      CF12(12)
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'CEP007'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 31'通路產品對照維護'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  6  2'公司代號:'
     A                                      COLOR(BLU)
     A            DEG01     R        B  6 14REFFLD(EG01)
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            D#B03     R   -6   O  6 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  8  2'通路代號:'
     A                                      COLOR(BLU)
     A            DEG02     R        B  8 14REFFLD(EG02)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A            DND04     R        O  8 17REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                 10  2'公司產品代號:'
     A                                      COLOR(BLU)
     A            DEG03     R        B 10 18REFFLD(EG03)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            DNC05     R        O    +1REFFLD(NC05 MANCPF)
     A                                      COLOR(BLU)
     A                                 12  2'生效起日:'
     A                                      COLOR(BLU)
     A            DEG04     R   -2   B 12 14REFFLD(EG04)
     A                                      EDTCDE(Y)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 14  2'生效訖日:'
     A                                      COLOR(BLU)
     A            DEG05     R   -2   B 14 14REFFLD(EG05)
     A                                      EDTCDE(Y)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 16  2'通路產品代號:'
     A                                      COLOR(BLU)
     A            DEG06     R        B 16 18REFFLD(EG06)
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 18'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 34'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
      *----------------------------------------------------------------
     A          R DSPC2
     A                                 22  2'F3 =回主畫面'
     A                                      COLOR(BLU)
     A  71                             22 18'F9 =確定存檔'
     A                                      COLOR(BLU)
     A                                 22 34'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
