     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PRTF NAME    : PUC061P                                    *
     A*    AUTHOR       : A1858 RACHEL HSIEH                         *
     A*    CREATE DATE  : 110/09/30                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC061                                     *
     A*    SYSTEM       :                                          *
     A*    SUBSYSTEM    :採購系統                                  *
     A*    REMARK       :惠康調撥驗收月報表                        *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A****************************************************************
     A                                      REF(SCRF)
     A          R PH1
     A                                      SKIPB(002)
     A                                     1
     A                                      '<PUC061P>'
     A                                    22
     A                                      MSGCON(030 USC9999 SCMF)
     A                                    56
     A                                      '日期:'
     A            $EGMDY         6  0O    +1
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                    28
     A                                      '調撥驗收月報表'
     A                                    56
     A                                      '時間:'
     A                                    +1
     A                                      TIME
     A                                      SPACEA(001)
     A                                    56
     A                                      '頁次:'
     A                                    +1
     A                                      PAGNBR
     A                                      EDTCDE(Z)
     A                                      SPACEA(001)
     A                                     1
     A                                      '通路代號: '
     A            DBG02     R        O    +1
     A                                      REFFLD(CK01)
     A            DND04     R        O    +1
     A                                      REFFLD(#B03 PA#BPF)
     A                                    +6
     A                                      '驗收日期:'
     A            DBG03S         6  0O    +1
     A                                      EDTCDE(Y)
     A                                    +1
     A                                      '-'
     A            DBG03E         6  0O    +1
     A                                      EDTCDE(Y)
     A                                      SPACEA(001)
     A                                     1
     A                                      '分店代號:'
     A            DBG04S         5   O    +1
     A                                    +1
     A                                      '-'
     A            DBG04E         5   O    +1
     A                                   +13
     A                                      '產品代號:'
     A            DBG05S         9   O    +1
     A                                    +1
     A                                      '-'
     A            DBG05E         9   O    +1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEB(001)
     A                                     1
     A                                      '收貨分店'
     A                                      SPACEB(001)
     A                                    +1
     A                                      '分店名稱'
     A                                    +2
     A                                      '產品代號'
     A                                    +1
     A                                      '產品名稱'
     A                                   +12
     A                                      '驗收數量'
     A                                     1
     A                                      '-----------------------------------
     A                                      ------------------------------------
     A                                      --'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PD1
     A                                      SPACEB(001)
     A  31        WF02           5         3
     A  31        WF03          10O       12
     A            WF04           9        25
     A            WF05          18O       35
     A            WF06           6  0     59
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PD2
     A                                      SPACEB(001)
     A                                    39
     A                                      '分店   合計:'
     A            SUB            7  0     +2
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PD3
     A                                      SPACEB(001)
     A                                    39
     A                                      '         總計:'
     A            TOT            7  0     +2
     A                                      EDTCDE(1)
     A*----------------------------------------------------------------
     A          R PE1
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEB(001)
     A                                     2
     A                                      '***續下頁***'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE2
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      =='
     A                                      SPACEB(001)
     A                                     2
     A                                      '<<PUC061P>>--->報表結束'
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE3
     A                                     1
     A                                      '==================================-
     A                                      ===================================-
     A                                      ='
     A                                      SPACEB(001)
     A*----------------------------------------------------------------
     A          R PE9
     A                                      SPACEB(001)
     A                                     3
     A                                      'もも此範圍無資料もも'
