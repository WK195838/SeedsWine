     A*%%TS  SD  20210629  105013  SCPGMR      REL-V5R4M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC051D                                    *
     A*    AUTHOR       : CAROL                                      *
     A*    CREATE DATE  : 109/10/15                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC051D                                    *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :調撥單維護 (A/U/D/I)                      *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '2' '4' '5' '6')
     A  69                                  DSPATR(PC)
     A  69                                  DSPATR(RI)
     A            DTC03          6Y 0O 10  7
     A                                      EDTWRD('  /  /  ')
     A            TC04      R        O 10 17
     A            DNE051    R        O 10 23REFFLD(NE05)
     A            TC05      R        O 10 34
     A            TC07      R        O 10 49
     A            DNE052    R        O 10 55REFFLD(NE05)
     A            DTC06          1A  O 10 68
     A*
     A            HTC09     R        H      REFFLD(TC09)
     A            HTC08     R        H      REFFLD(TC08)
     A            HTC03          6Y 0H
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      CF06(06)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'PUC051'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'　調撥單維護　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 14REFFLD(TC01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(TC02)
     A                                      DSPATR(HI)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 18'2=修改'
     A                                      COLOR(BLU)
     A                                  5 27'4=刪除'
     A                                      COLOR(BLU)
     A                                  5 36'5=查詢'
     A                                      COLOR(BLU)
     A                                  5 45'6=核准'
     A                                      COLOR(BLU)
     A                                  7  3'（月／日／年）'
     A                                      COLOR(BLU)
     A                                  8  3'調撥出貨日期發貨分店／名稱 +
     A                                       調撥單號     收貨分店／名稱 +
     A                                      狀態'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN3     R   -2   B  9  7REFFLD(TC06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            DBGN4     R        B  9 34REFFLD(TC05)
     A                                      DSPATR(HI)
     A            DBGN5     R        B  9 49REFFLD(TC07)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3=離開'
     A                                      COLOR(BLU)
     A                                    +2'F6=新增'
     A                                      COLOR(BLU)
     A  70                             24  2'單據號碼'
     A                                      DSPATR(HI)
     A  70        DTC05     R             +1REFFLD(TC05)
     A                                      DSPATR(RI)
     A  71                                +1'新增成功'
     A                                      DSPATR(HI)
     A  72                             24 27'刪除成功'
     A                                      COLOR(RED)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DOPT2          1A  B 11  4VALUES(' ' '1' '2' '4')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A  34                                  DSPATR(UL)
     A N34                                  DSPATR(PR)
     A            DTD02     R        B 11  7REFFLD(TD02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A  32                                  DSPATR(UL)
     A N32                                  DSPATR(PR)
     A            NC05      R        O 11 18
     A            NC14      R        O 11 38
     A            HTD04     R        H      REFFLD(TD04)
     A            DTD04     R        B 11 45REFFLD(TD04)
     A                                      EDTCDE(1)
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A  33                                  DSPATR(UL)
     A N33                                  DSPATR(PR)
     A            DCHK           1A  H
     A*------------------------------------------------------------
     A*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A  33                                  CF04(04)
     A                                      CF05(05)
     A                                      CF09(09)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC051'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'　調撥單維護　'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DTC01     R        O  4 14REFFLD(TC01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DTC02     R        O  4 43REFFLD(TC02)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'調撥單號:'
     A                                      COLOR(BLU)
     A            DTC05     R        O  5 14REFFLD(TC05)
     A                                  6  2'出貨分店:'
     A                                      COLOR(BLU)
     A            DTC04     R        O  6 14REFFLD(TC04)
     A* 65                                  DSPATR(PC)
     A* 65                                  DSPATR(RI)
     A* 35                                  DSPATR(UL)
     A*N35                                  DSPATR(PR)
     A            DNE051    R        O  6 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A                                  6 39'調撥出貨日期:'
     A                                      COLOR(BLU)
     A            DTC03     R    6Y 0B  6 55REFFLD(TC03)
     A                                      EDTWRD('  /  /  ')
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A  31                                  DSPATR(UL)
     A N31                                  DSPATR(PR)
     A                                  6 64'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7  2'收貨分店:'
     A                                      COLOR(BLU)
     A            DTC07     R        B  7 14REFFLD(TC07)
     A  61                                  DSPATR(PC)
     A  61                                  DSPATR(RI)
     A  30                                  DSPATR(UL)
     A N30                                  DSPATR(PR)
     A            DNE052    R        O  7 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A*                                 7 39'入庫日期:'
     A*                                     COLOR(BLU)
     A*           DTC08     R    6Y 0B  7 51REFFLD(TC03)
     A*                                     EDTWRD('  /  /  ')
     A* 66                                  DSPATR(PC)
     A* 66                                  DSPATR(RI)
     A* 31                                  DSPATR(UL)
     A*N31                                  DSPATR(PR)
     A* 67                                  DSPATR(ND)
     A*                                 7 60'(月／日／年)'
     A*                                     COLOR(BLU)
     A                                  8  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  8 18'1=新增'
     A                                      COLOR(BLU)
     A                                  8 27'2=修改'
     A                                      COLOR(BLU)
     A                                  8 36'4=刪除'
     A                                      COLOR(BLU)
     A                                 10  3' A 產品代號 產品簡稱          +
     A                                        單位　調撥數量'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A  35                             10 54'(已核准)'
     A*------------------------------------------------------------
     A*
     A          R DSPC2
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3=回選單'
     A                                      COLOR(BLU)
     A  33                                +1'F4=視窗查詢'
     A                                      COLOR(BLU)
     A  33                                +1'F5=參考資料'
     A                                      COLOR(BLU)
     A  80                                +1'F9=確認存檔'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
