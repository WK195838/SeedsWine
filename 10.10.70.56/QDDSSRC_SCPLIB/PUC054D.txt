     A*%%TS  SD  20210930  111057  SCPGMR      REL-V5R4M0  5722-WDS
     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*--------------------------------------------------------------*
     A*    DSPF NAME    : PUC054D                                    *
     A*    AUTHOR       : RACHEL                                     *
     A*    CREATE DATE  : 110/09/28                                  *
     A*    UPDATE DATE  :                                            *
     A*    PROGRAM NAME : PUC054D                                    *
     A*    SYSTEM       :西祺進銷存                                *
     A*    SUBSYSTEM    :庫存管理系統                              *
     A*    REMARK       :調撥單歷史查詢                            *
     A*===============================================================
     A* MODI CODE |   DATE   | DESCRIPTION
     A*---------------------------------------------------------------
     A*---------------------------------------------------------------
     A****************************************************************
     A*%%EC
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(24)
     A                                      PRINT
     A*------------------------------------------------------------
     A*
     A          R SFLSR1                    SFL
     A  54                                  SFLNXTCHG
     A            RRN1           4S 0H
     A            DOPT1          1A  B 10  4VALUES(' ' '5')
     A  60                                  DSPATR(PC)
     A  60                                  DSPATR(RI)
     A            DTC03          6Y 0O 10  7
     A                                      EDTWRD('  /  /  ')
     A            TC04      R        O 10 17
     A            DNE051    R        O 10 23REFFLD(NE05)
     A            TC05      R        O 10 34
     A            TC07      R        O 10 49
     A            DNE052    R        O 10 55REFFLD(NE05)
     A            DTC09          1A  O 10 68COLOR(PNK)
     A            DTC09A         1A  O 10 75COLOR(PNK)
     A*
     A            HTC09     R        H      REFFLD(TC09)
     A            HTC08     R        H      REFFLD(TC08)
     A            HTC03          6Y 0H
     A*------------------------------------------------------------
     A*
     A          R SFLCR1                    SFLCTL(SFLSR1)
     A                                      SFLSIZ(0020)
     A                                      SFLPAG(0010)
     A                                      ROLLUP(25 '下一頁')
     A                                      CA03(03)
     A                                      OVERLAY
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN1          4S 0H      SFLRCDNBR
     A                                  1  2'PUC054'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 32'調撥單歷史查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DBGN1     R        B  4 14REFFLD(TC01)
     A                                      DSPATR(HI)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DBGN2     R        B  4 43REFFLD(TC02)
     A                                      DSPATR(HI)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'輸入作業選擇:'
     A                                      COLOR(BLU)
     A                                  5 18'5=查詢'
     A                                      COLOR(BLU)
     A                                  7  3'（月／日／年）'
     A                                      COLOR(BLU)
     A                                  8  3'調撥出貨日期發貨分店／名稱 +
     A                                       調撥單號     收貨分店／名稱'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  8 66'驗收'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A                                  8 73'入庫'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A            DBGN3     R   -2   B  9  7REFFLD(TC06)
     A                                      EDTWRD('  /  /  ')
     A                                      DSPATR(HI)
     A            DBGN4     R        B  9 34REFFLD(TC05)
     A                                      DSPATR(HI)
     A            DBGN5     R        B  9 49REFFLD(TC07)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A          R DSPC1
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +2'F3=回主畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A*------------------------------------------------------------
     A*
     A          R SFLSR2                    SFL
     A  54                                  SFLNXTCHG
     A            RRN2           4S 0H
     A            DTD02     R        O 11  7REFFLD(TD02)
     A            NC05      R        O 11 18
     A            DTD03     R        O 11 38REFFLD(TD03)
     A                                      EDTCDE(L)
     A            HTD04     R        H      REFFLD(TD04)
     A            DTD04     R        O 11 46REFFLD(TD04)
     A                                      EDTCDE(L)
     A            DCHK           1A  H
     A*------------------------------------------------------------
     A*
     A          R SFLCR2                    SFLCTL(SFLSR2)
     A                                      SFLSIZ(0200)
     A                                      SFLPAG(0010)
     A                                      CSRLOC(D#ROW      D#COL)
     A                                      CA03(03)
     A                                      CA12(12)
     A                                      OVERLAY
     A  37                                  FRCDTA
     A  37                                  LOCK
     A  50                                  SFLDSP
     A  51                                  SFLDSPCTL
     A  52                                  SFLCLR
     A  53                                  SFLEND
     A  98                                  SFLMSGID(CPF5203 *LIBL/PTMF)
     A            DRRN2          4S 0H      SFLRCDNBR
     A            D#ROW          3S 0H
     A            D#COL          3S 0H
     A                                  1  2'PUC054'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 62'日期:'
     A            $EGMDY         6Y 0O  1 70EDTCDE(Y)
     A                                  2  2'SCR002'
     A                                  2 32'調撥單歷史查詢'
     A                                      DSPATR(HI)
     A                                  2 62'時間:'
     A                                  2 70TIME
     A                                      EDTWRD('  :  :  ')
     A            DFUN           6O  O  3  2DSPATR(RI)
     A                                  3 62' USER :'
     A            $USER         10A  O  3 70
     A                                  4  2'公司代號:'
     A                                      COLOR(BLU)
     A            DTC01     R        O  4 14REFFLD(TC01)
     A            D#B03     R   -4   O  4 17REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  4 31'通路代號:'
     A                                      COLOR(BLU)
     A            DTC02     R        O  4 43REFFLD(TC02)
     A            DND04     R        O  4 46REFFLD(ND04)
     A                                      COLOR(BLU)
     A                                  5  2'調撥單號:'
     A                                      COLOR(BLU)
     A            DTC05     R        O  5 14REFFLD(TC05)
     A                                  6  2'出貨分店:'
     A                                      COLOR(BLU)
     A            DTC04     R        O  6 14REFFLD(TC04)
     A            DNE051    R        O  6 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A                                  6 39'調撥出貨日期:'
     A                                      COLOR(BLU)
     A            DTC03     R    6Y 0O  6 55REFFLD(TC03)
     A                                      EDTWRD('  /  /  ')
     A                                  6 64'(月／日／年)'
     A                                      COLOR(BLU)
     A                                  7  2'收貨分店:'
     A                                      COLOR(BLU)
     A            DTC07     R        O  7 14REFFLD(TC07)
     A            DNE052    R        O  7 20REFFLD(NE05)
     A                                      COLOR(BLU)
     A  31                              7 39'驗收  日期:'
     A                                      COLOR(BLU)
     A  32                              7 39'入庫  日期:'
     A                                      COLOR(BLU)
     A  31
     AO 32                              7 64'(月／日／年)'
     A                                      COLOR(BLU)
     A            DTC06     R    6Y 0O  7 55REFFLD(TC06)
     A                                      EDTWRD('  /  /  ')
     A                                 10  3'   產品代號 產品簡稱          +
     A                                        調撥量　驗收量'
     A                                      COLOR(BLU)
     A                                      DSPATR(UL)
     A N31N32                          10 54'(核准並產生出貨單)'
     A*                                     COLOR(PNK)
     A  31                             10 54'(已驗收)'                       +
     A                                      COLOR(PNK)
     A  32                             10 54'(已入庫)'                       +
     A                                      COLOR(PNK)
     A*------------------------------------------------------------
     A*
     A          R DSPC2
     A                                 22  2'執行'
     A                                      COLOR(BLU)
     A                                    +1'F3=回選單'
     A                                      COLOR(BLU)
     A                                    +1'F12=回前畫面'
     A                                      COLOR(BLU)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A  97        DMSG          60A  O 24  2DSPATR(HI)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
