     A****************************************************************
     A*   泛太資訊科技開發股份有限公司－版權所有    TEL:7313250    *
     A*                                                              *
     A*    PROGRAM-ID   : PUC050D                                    *
     A*    PROGRAM NAME :調撥處理作業                              *
     A*    AUTHOR       : A1858 RACHEL                               *
     A*    CREATE DATE  : 2021/04/09                                 *
     A*    UPDATE DATE  :                                            *
     A*    SYSTEM       :西祺進銷存                                *
     A****************************************************************
     A                                      DSPSIZ(24 80 *DS3)
     A                                      REF(*LIBL/SCRF)
     A                                      MSGLOC(22)
     A                                      PRINT
     A          R DSPD1
     A                                      CSRLOC(#LIN       #COL)
     A                                      CA03(03)
     A*                                     CF04(04)
     A                                      CF14(14)
     A  28                                  FRCDTA
     A  28                                  LOCK
     A            #LIN           3S 0H
     A            #COL           3S 0H
     A                                  1  2'<PUC050D>'
     A                                  1 26MSGCON(030 USC9999 *LIBL/SCMF)
     A                                      DSPATR(HI)
     A                                  1 63'日期:'
     A            $EGMDY         6Y 0O  1 71EDTCDE(Y)
     A                                  2  2'SCR001'
     A                                  2 33'調撥處理作業'
     A                                      DSPATR(HI)
     A                                  2 63'時間:'
     A                                  2 71TIME
     A                                      EDTWRD('  :  :  ')
     A                                  3 63' USER :'
     A            $USER         10A  O  3 71
     A                                  6 28'公司代號:'
     A                                      COLOR(BLU)
     A            DBG01     R        B  6 40REFFLD(NI01)
     A  62                                  DSPATR(PC)
     A  62                                  DSPATR(RI)
     A            S#B03     R   -6   O  6 43REFFLD(#B03 PA#BPF)
     A                                      COLOR(BLU)
     A                                  8 28'通路代號:'
     A                                      COLOR(BLU)
     A            DBG02     R        B  8 40REFFLD(NI02)
     A  63                                  DSPATR(PC)
     A  63                                  DSPATR(RI)
     A            SND04     R        O  8 43REFFLD(ND04 MANDPF)
     A                                      COLOR(BLU)
     A                                 10 28'調撥出貨日期:'
     A                                      COLOR(BLU)
     A            DCD01          6  0B 10 44EDTWRD('  /  /  ')
     A  64                                  DSPATR(PC)
     A  64                                  DSPATR(RI)
     A                                 10 53'(MM/DD/YY)'
     A                                      COLOR(BLU)
     A                                 12 28'推算銷售起日:'
     A                                      COLOR(BLU)
     A            DCD02          6  0B 12 44EDTWRD('  /  /  ')
     A  65                                  DSPATR(PC)
     A  65                                  DSPATR(RI)
     A                                 12 53'(MM/DD/YY)'
     A                                      COLOR(BLU)
      *
     A  28                             21 28'***調撥處理完成!請參閱報表+
     A                                      ***'
     A                                      DSPATR(RI)
     A                                      DSPATR(BL)
     A  96        ERRMG2        78A  O 22  2
     A                                      COLOR(RED)
     A            ERRID          7A  H
     A            ERRF          10A  H
     A  99        ERRMSG        60A  O 24  2MSGID(&ERRID &ERRF)
     A                                      DSPATR(HI)
     A                                 23  2'執行'
     A                                      COLOR(BLU)
     A                                 23 10'F3 =回主畫面'
     A                                      COLOR(BLU)
     A                                 23 26'F14 =處理作業'
     A                                      COLOR(BLU)
     A                                 24 66MSGCON(014 USC9998 *LIBL/SCMF)
     A                                      DSPATR(HI)
