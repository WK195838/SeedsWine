     A*****************************************************************
     A*        庫存異動明細檔(INTOLF03) FOR庫存月結計算單位成本
     A*================================================================
     A* UTL2PF 代碼值設定檔
     A* L201 = INTOLF
     A* L202 = INTOLF 'S RECORD NAME (EX. CM0, TD0..)
     A* L203 = 第一碼:'+' -該數量屬 進貨
     A*                 '-' -該數量屬 出貨
     A*                 ' ' -
     A*                 '*' -該數量為正者　屬進貨
     A*                      該數量為負者　屬出貨
     A*                 '@' -該數量為正者　屬出貨
     A*                      該數量為負者　屬進貨
     A*
     A*================================================================
     A*序號|代號|     檔　案　名　稱      |  L203  | RCD NAME|
     A*======|======|===========================|========|=========|
     A*  01  |POCMPF|採購驗收明細檔- C1/C2    |'+'     | CM0     |
     A*  02  |INTHPF|陳列轉進貨明細檔         |'+'     | TH0     |
     A*  03  |INTMPF|產品成本調整資料檔       |'*'     | TM0     |
     A*  04  |INTPWF|銷售陳列借貨工作檔       |        | TPW     |
     A*****************************************************************
     A*
     A*--------------------------------------
     A* 採購驗收明細檔(POCMPF) C1一般/C2客訂
     A*--------------------------------------
     A          R CM0                       PFILE(POCMPF)
     A            TO01      R               COLHDG('公司別')
     A                                      RENAME(CM11)
     A            TO04      R               COLHDG('產品代號')
     A                                      RENAME(CM03)
     A            TO05      R               COLHDG('到貨運日期')
     A                                      RENAME(CM16)
     A            TO06           2   I      COLHDG('單別')
     A                                      SST(CM01 3 2)
     A            TO07      R               COLHDG('驗收單號')
     A                                      RENAME(CM01)
     A*
     A            TO101     R               COLHDG('單價')
     A                                      RENAME(CM07)
     A            TO121     R    6S 0       COLHDG('確認數量')
     A                                      RENAME(CM06)
     A            TO13      R               COLHDG('狀態碼')
     A                                      RENAME(CM15)
     A          K TO01
     A          K TO04
     A          K TO05
     A          K TO06
     A          K TO07
     A          O TO13                      COMP(NE 'P')
     A          S TO06                      COMP(EQ 'C1')
     A          S TO06                      COMP(EQ 'C2')
     A*
     A*--------------------------------------
     A* 陳列轉進貨明細檔(INTHPF)
     A*--------------------------------------
     A          R TH0                       PFILE(INTHPF)
     A            TO01      R               COLHDG('公司別')
     A                                      RENAME(TH06)
     A            TO04      R               COLHDG('產品編號')
     A                                      RENAME(TH02)
     A            TO05      R               COLHDG('轉進貨日期')
     A                                      RENAME(TH09)
     A            TO06           2   I      COLHDG('單別')
     A                                      SST(TH01 3 2)
     A            TO07      R               COLHDG('轉進貨單號')
     A                                      RENAME(TH01)
     A*
     A            TO101     R               COLHDG('單價')
     A                                      RENAME(TH04)
     A            TO121     R    6S 0       COLHDG('轉進貨量')
     A                                      RENAME(TH03)
     A            TO13      R               COLHDG('狀態碼')
     A                                      RENAME(TH10)
     A          K TO01
     A          K TO04
     A          K TO05
     A          K TO06
     A          K TO07
     A          O TO13                      COMP(NE 'P')
     A*
     A*--------------------------------------
     A* 產品成本調整資料檔 (INTMPF)
     A*--------------------------------------
     A          R TM0                       PFILE(INTMPF)
     A            TO01      R               COLHDG('公司別')
     A                                      RENAME(TM01)
     A            TO04      R               COLHDG('產品代號')
     A                                      RENAME(TM03)
     A            TO05      R               COLHDG('調整日期')
     A                                      RENAME(TM02)
     A            TO06           2   I      COLHDG('單別')
     A                                      SST(TM08 3 2)
     A            TO07      R               COLHDG('調整單號')
     A                                      RENAME(TM08)
     A*
     A            TO104     R               COLHDG('調整金額')
     A                                      RENAME(TM05)
     A            TO13      R               COLHDG('狀態碼')
     A                                      RENAME(TM07)
     A          K TO01
     A          K TO04
     A          K TO05
     A          K TO06
     A          K TO07
     A          O TO13                      COMP(NE 'P')
     A*
     A*--------------------------------------
     A* 銷售陳列借貨工作檔(INTPWF)
     A*--------------------------------------
     A          R TPW                       PFILE(INTPWF)
     A            TO01      R               COLHDG('公司別')
     A                                      RENAME(TPW01)
     A            TO04      R               COLHDG('產品編號')
     A                                      RENAME(TPW02)
     A            TO05      R               COLHDG('銷借日期')
     A                                      RENAME(TPW03)
     A            TO06           2   I      COLHDG('單別')
     A                                      SST(TPW04 3 2)
     A            TO07      R               COLHDG('銷陳工作單號')
     A                                      RENAME(TPW04)
     A*
     A            TO101     R               COLHDG('單價')
     A                                      RENAME(TPW05)
     A            TO121     R    6S 0       COLHDG('轉進貨量')
     A                                      RENAME(TPW06)
     A            TO13      R               COLHDG('狀態碼')
     A                                      RENAME(TPW07)
     A          K TO01
     A          K TO04
     A          K TO05
     A          K TO06
     A          K TO07
     A*
